plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.github.triplet.play' version '2.3.0'
}

ext {
    // Themers: DO NOT MODIFY
    byte[] key = new byte[16]
    new Random().nextBytes(key)
    KEY = key
    byte[] iv = new byte[16]
    new Random().nextBytes(iv)
    IV_KEY = iv
}

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.schnettler.outlinecolors"
        targetSdkVersion 29

        versionCode 38
        versionName "13.3"

        buildConfigField "String", "IV_KEY", "\"" + IV_KEY + "\""
        buildConfigField "byte[]", "DECRYPTION_KEY", String.valueOf("\"" + KEY + "\"").replace("\"", "").replace("[", "{").replace("]", "}")
        buildConfigField "byte[]", "IV_KEY", String.valueOf("\"" + IV_KEY + "\"").replace("\"", "").replace("[", "{").replace("]", "}")
    }

    signingConfigs {
        outlineXRelease {
            storeFile file("OutlineXKey.jks")
            storePassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_STORE_PASSWORD
            keyAlias "outlinecolors"
            keyPassword System.getenv("SIGNING_KEY_PASSWORD") // ANDROID_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.outlineXRelease
        }
    }

    flavorDimensions "android"
    productFlavors {
        nougat {
            minSdkVersion 25
            versionCode 2500 + android.defaultConfig.versionCode
            dimension "android"
            versionNameSuffix "N"
        }
        oreo {
            minSdkVersion 26
            versionCode 2600 + android.defaultConfig.versionCode
            dimension "android"
            versionNameSuffix "O"
        }
        oreoMr {
            minSdkVersion 27
            versionCode 2700 + android.defaultConfig.versionCode
            dimension "android"
            versionNameSuffix "O-MR"
        }
        pancake {
            minSdkVersion 28
            versionCode 2800 + android.defaultConfig.versionCode
            dimension "android"
            versionNameSuffix "P"
        }
        q {
            minSdkVersion 29
            versionCode 2900 + android.defaultConfig.versionCode
            dimension "android"
            versionNameSuffix "Q"
        }
    }

    playConfigs {
        q {
            commit = true
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}

play {
    serviceAccountCredentials = file("PlayAPI.json")
    track = "production"
    commit = false
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version")
    implementation("androidx.appcompat:appcompat:1.0.2")
    implementation("androidx.preference:preference:1.0.0")
    implementation("androidx.constraintlayout:constraintlayout:1.1.3")
    implementation("com.google.android.material:material:1.1.0-alpha02")
}